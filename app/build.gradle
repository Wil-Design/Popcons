apply plugin: 'com.android.application'

android {
    compileSdk rootProject.ext.CompileSdk

    namespace 'com.wil.popcons'

    defaultConfig {
        applicationId 'com.wil.popcons'
        minSdkVersion rootProject.ext.MinSdk
        targetSdkVersion rootProject.ext.TargetSdk
        versionCode 16
        versionName '1.6'
    }

    bundle {
        language {
            enableSplit false
        }
    }

    signingConfigs {
        release {
            storeFile file("${rootDir}/keystore.jks")
            storePassword System.getenv("KEYSTORE_PASSWORD")
            keyAlias System.getenv("KEY_ALIAS")
            keyPassword System.getenv("KEY_PASSWORD")
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            debuggable false
            minifyEnabled false
        }
    }

    lint {
        abortOnError false
    }
}

base {
    // Output file name
    archivesName = "${parent.name}-${android.defaultConfig.versionCode}"
}
java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
}


dependencies {
    /*
     * CandyBar Material Dashboard Library
     *
     * TODO: How to update the dashboard?!
     * Check this update guide for every version
     *      - https://github.com/zixpo/candybar-sample/wiki/Updating-the-dashboard
     * If you've missed some versions you've to also follow update guide for those versions
     */

    implementation 'com.github.Donnnno:candybar-foss:3.21.1'

    // TODO: Remove `//` below to enable OneSignal
    //implementation 'com.onesignal:OneSignal:[5.0.0, 5.99.99]'

}

// Code for automatically copying appfilter.xml and drawable.xml
// from `res/xml` directory to `assets` directory
tasks.register('copyXMLs') {
    ['appfilter.xml', 'drawable.xml'].each { file ->
        copy {
            from "$projectDir/src/main/res/xml/$file"
            into "$projectDir/src/main/assets/"
        }
    }
}
tasks.whenTaskAdded { task ->
    if (task.name == 'assembleDebug' || task.name == 'assembleRelease') {
        task.dependsOn copyXMLs
    }
}
